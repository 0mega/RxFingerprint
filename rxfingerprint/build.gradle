/*
 * Copyright 2015 Marvin Ramin
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.0.0-alpha3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.4"
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

group 'com.mtramin'
version '0.1.1'
def libName = 'RxFingerprint'
def libDescription =  'Android library to use fingerprint authentication with RxJava'
def mavenPackageType = 'aar'
def mavenDeveloperId = 'mauin'
def mavenDeveloperName = 'Marvin Ramin'
def mavenDeveloperEmail = 'marvin.ramin@gmail.com'
def libWebsite = "https://github.com/Mauin/RxFingerprint"
def libGit = "https://github.com/Mauin/RxFingerprint.git"


def versionTag = version.toString()

bintray {
    user = bintray_user
    key = bintray_key
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = libName
        desc = libDescription
        websiteUrl = libWebsite
        issueTrackerUrl = "https://github.com/Mauin/RxFingerprint/issues"
        vcsUrl = libGit
        licenses = ['Apache-2.0']
        labels = ['aar', 'android', 'fingerprint', 'fingerprintmanager', 'rxjava', 'authentication']
        version {
            name = versionTag
            released = new Date()
            vcsTag = versionTag
        }
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            packaging mavenPackageType
            name libName
            url libWebsite
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id mavenDeveloperId
                    name mavenDeveloperName
                    email mavenDeveloperEmail
                }
            }
            scm {
                connection libGit
                developerConnection libGit
                url libWebsite

            }
        }
    }
}


android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 23
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'io.reactivex:rxjava:1.0.16'
}
